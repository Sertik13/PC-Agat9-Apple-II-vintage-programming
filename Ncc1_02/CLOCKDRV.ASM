

**********************************************************
*                                                        *
*      Драйвер ввода/корректировки текущего времени      *
* ______________________________________________________ *
*                                                        *
*   (C) 1993, Ниппель, by ALV Software, Автор: А.Голов   *
*                                                        *
**********************************************************


*========================================================*
*          Вызов драйвера управления часами              *
* ------------------------------------------------------ *
*  Вход: X - команда драйверу управления часами          *
*    0 - фиксация текущей информации                     *
*    1 - запрос информации о времени                     *
*    2 - запрос информации о дате                        *
*    3 - запрос информации о дне недели                  *
*    4 - установка нового времени                        *
*    5 - установка новой даты                            *
*    6 - установка нового дня недели                     *
*  $FF - инициализация драйвера часов                    *
*  ----------------------------------------------------  *
*  Выход: В соответствии с командой                      *
*========================================================*
DCLOCK  CPX #$FF        Инициализация
        BEQ DCLKINT     = Выполнение инициализации
        CPX #7
        BCS DCLOCKE     = Нет такой команды
        TXA             - Вызов подпрограммы
        ASL A
        TAX
        LDA DCLOCKA,X
        PHA
        LDA DCLOCKA+1,X
        PHA
DCLOCKE RTS             - ;

*--------------------------------------*
* Список подпрограмм исполнения команд *
*--------------------------------------*
DCLOCKA DDB FIXPAR-1  Фиксация информации
        DDB INQTIM-1  Запрос времени
        DDB INQDAT-1  Запрос даты
        DDB INQDAY-1  Запрос дня недели
        DDB SETTIM-1  Установка времени
        DDB SETDAT-1  Установка даты
        DDB SETDAY-1  Установка дня недели

*--------------------------------*
*  Инициализация драйвера часов  *
*--------------------------------*
DCLKINT  LDA #$10
DCLKINT0 STA CSLOT
         JSR DCLKINV    Проверить на этом разъеме
         BCS DCLKINT1   = Здесь нет часов
         LDY #$A        - Инициализировать регистр A
         LDA #:00101111 Тип кварца и прерывания 2 Гц.
         JSR CWRBYTE    - ;
         LDY #$B        - Инициализировать регистр B
         LDA #:00000110 Данные в двоичном, ход 24 часа
         JSR CWRBYTE    прерывания запрещены - ;
         JMP FIXPAR     = Зафиксировать время
DCLKINT1 LDA CSLOT      - Перейти к следующему разъему
         CLC
         ADC #$10
         CMP #$70
         BCC DCLKINT0   - ;
         LDA #0         - Признак отсутствия часов
         STA CSLOT      - ;
         RTS

*-----------------------------*
*  Проверка текущего разъема  *
*-----------------------------*
DCLKINV  LDY #$E      - Сохранить ячейки $E и $F
         JSR CRDBYTE  Прочитать байт
         STA CWORK0
         INY
         JSR CRDBYTE  Прочитать байт
         STA CWORK1   - ;
         LDA #2       - Записать #2 в ячейку $F
         JSR CWRBYTE  Записать байт - ;
         DEY          - Записать #1 в ячейку $E
         LDA #1
         JSR CWRBYTE  Записать байт - ;
         JSR CRDBYTE  - Проверить записалось ли
         CMP #1
         BNE DCLKINVR = Не записалось
         INY
         JSR CRDBYTE  Прочитать байт
         CMP #2
         BNE DCLKINVR = Не записалось - ;
         LDA #4       - Записать #4 в ячейку $F
         JSR CWRBYTE  Записать байт
         DEY          - Записать #3 в ячейку $E
         LDA #3
         JSR CWRBYTE  Записать байт - ;
         JSR CRDBYTE  - Проверить записалось ли
         CMP #3
         BNE DCLKINVR = Не записалось
         INY
         JSR CRDBYTE  Прочитать байт
         CMP #4
         BNE DCLKINVR = Не записалось
         CLC          -- Часы найдены
         DFB @44      Пропуск SECа
DCLKINVR SEC          -- Часов нет
DCLKINVE LDY #$E      - Востановить ячейки $E и $F
         LDA CWORK0
         JSR CWRBYTE  Записать байт
         INY
         LDA CWORK1
         JMP CWRBYTE  Записать байт - ; -- ;

*----------------------------------*
*  Фиксация текущего времени/даты  *
* -------------------------------- *
*  Вход: -//-                      *
*  ------------------------------- *
*  Выход: информация зафиксирована *
*----------------------------------*
FIXPAR  LDA CSLOT
        BEQ FIXPARE    = Часов нет
FIXPAR0 LDY #$A        - Прочитать бит готовности
        JSR CRDBYTE
        AND #:10000000 'UIP'
        BNE FIXPAR0    = Информация не готова
        LDY #4         - Часы
        JSR CRDBYTE    Прочитать байт
        STA CFIXDCLK   - ;
        LDY #2         - Минуты
        JSR CRDBYTE    Прочитать байт
        STA CFIXDMIN   - ;
        LDY #0         - Секунды
        JSR CRDBYTE    Прочитать байт
        STA CFIXDSEC   - ;
        LDY #9         - Год
        JSR CRDBYTE    Прочитать байт
        STA CFIXDYER   - ;
        LDY #8         - Месяц
        JSR CRDBYTE    Прочитать байт
        STA CFIXDMON   - ;
        LDY #7         - Число
        JSR CRDBYTE    Прочитать байт
        STA CFIXDNUM   - ;
        LDY #6         - День недели
        JSR CRDBYTE    Прочитать байт
        STA CFIXDDAY   - ;
FIXPARE RTS

*-------------------------------------*
*       Запрос текущего времени       *
* ----------------------------------- *
*  Вход: зафиксированное ранее время  *
*  ---------------------------------  *
*  Выход: CLPARM1 - час               *
*         CLPARM2 - минута            *
*         CLPARM3 - секунда           *
*-------------------------------------*
INQTIM LDA CFIXDCLK - Час
       STA CLPARM1  - ;
       LDA CFIXDMIN - Минута
       STA CLPARM2  - ;
       LDA CFIXDSEC - Секунда
       STA CLPARM3  - ;
       RTS

*------------------------------------*
*        Запрос текущей даты         *
* ---------------------------------- *
*  Вход: зафиксированная ранее дата  *
*  --------------------------------  *
*  Выход: CLPARM1 - число            *
*         CLPARM2 - месяц            *
*         CLPARM3 - год              *
*------------------------------------*
INQDAT LDA CFIXDYER - Год
       STA CLPARM3  - ;
       LDA CFIXDMON - Месяц
       STA CLPARM2  - ;
       LDA CFIXDNUM - Число
       STA CLPARM1  - ;
       RTS

*-------------------------------------------*
*        Запрос текущего дня недели         *
* ----------------------------------------- *
*  Вход: зафиксированный ранее день недели  *
*  ---------------------------------------  *
*  Выход: CLPARM1 - день недели             *
*-------------------------------------------*
INQDAY LDA CFIXDDAY
       STA CLPARM1
       RTS

*---------------------------------*
*    Установка нового времени     *
* ------------------------------- *
*  Вход: CLPARM1 - час            *
*        CLPARM2 - минута         *
*        CLPARM3 - секунда        *
*  -----------------------------  *
*  Выход: установлено             *
*---------------------------------*
SETTIM  LDA CSLOT
        BEQ SETTIME     = Часов нет
        JSR CSTOP       Остановить часы
        LDA CLPARM1     - Час
        STA CFIXDCLK
        LDY #4
        JSR CWRBYTE     Записать байт - ;
        LDA CLPARM2     - Минута
        STA CFIXDMIN
        LDY #2
        JSR CWRBYTE     Записать байт - ;
        LDA CLPARM3     - Секунда
        STA CFIXDSEC
        LDY #0
        JSR CWRBYTE     Записать байт - ;
        LDY #$A         - Сброс делителя отсчета секунды
        JSR CRDBYTE     Прочитать байт
        AND #:10001111
        ORA #:01110000
        LDY #$A         Регистр А
        JSR CWRBYTE     Записать байт
        LDY #$A         Регистр А
        JSR CRDBYTE     Прочитать байт
        AND #:10001111
        ORA #:00100000
        LDY #$A         Регистр А
        JSR CWRBYTE     Записать байт
        JSR CPUSK       Запустить часы
SETTIME RTS

*---------------------------------*
*      Установка новой даты       *
* ------------------------------- *
*  Вход: CLPARM1 - число          *
*        CLPARM2 - месяц          *
*        CLPARM3 - год            *
*  -----------------------------  *
*  Выход: установлено             *
*---------------------------------*
SETDAT  LDA CSLOT
        BEQ SETDATE   = Часов нет
        JSR CSTOP     Остановить часы
        LDA CLPARM3   - Год
        STA CFIXDYER
        LDY #9
        JSR CWRBYTE   Записать байт - ;
        LDA CLPARM2   - Месяц
        STA CFIXDMON
        LDY #8
        JSR CWRBYTE   Записать байт - ;
        LDA CLPARM1   - Число
        STA CFIXDNUM
        LDY #7
        JSR CWRBYTE   Записать байт - ;
        JSR CPUSK     Запустить часы
SETDATE RTS

*----------------------------------*
*    Установка нового дня недели   *
* -------------------------------- *
*  Вход: CLPARM1 - день недели     *
*  ------------------------------  *
*  Выход: установлено              *
*----------------------------------*
SETDAY  LDA CSLOT
        BEQ SETDAYE   = Часов нет
        JSR CSTOP     Остановить часы
        LDA CLPARM1   - День недели
        STA CFIXDDAY
        LDY #6
        JSR CWRBYTE   Записать байт - ;
        JSR CPUSK     Запустить часы
SETDAYE RTS

*------------------------*
*  Остановить ход часов  *
*------------------------*
CSTOP LDY #$A
      JSR CRDBYTE
      AND #:10000000
      BNE CSTOP       = Ожидание готовности
      LDY #$B
      JSR CRDBYTE     Прочитать байт
      ORA #:10000000
      JMP CWRBYTE     Записать байт

*-----------------------*
*  Запустить ход часов  *
*-----------------------*
CPUSK LDY #$B
      JSR CRDBYTE     Прочитать байт
      AND #:01111111
      JMP CWRBYTE     Записать байт

*----------------------------*
*  Чтение байта по адресу Y  *
*----------------------------*
CRDBYTE LDX CSLOT   Текущий разъем
        TYA
        STA $C086,X Порт адреса
        LDA $C087,X Порт данных
        RTS

*-----------------------------*
*  Запись данных по адресу Y  *
*-----------------------------*
CWRBYTE PHA
        LDX CSLOT   Текущий разъем
        TYA
        STA $C086,X Порт адреса
        PLA
        STA $C087,X Порт данных
        RTS

*-----------------------------------*
* Текущая информация о времени/дате *
*-----------------------------------*
CFIXDCLK DFB 23 Час 
CFIXDMIN DFB 30 Минута
CFIXDSEC DFB 00 Секунда
CFIXDYER DFB 69 Год
CFIXDMON DFB 07 Месяц
CFIXDNUM DFB 30 Число
CFIXDDAY DFB 3  День недели
CSLOT    DFB 0  Разъем контроллера *$10
CWORK0   DFB 0  Рабочие:
CWORK1   DFB 0

*========================================================*

